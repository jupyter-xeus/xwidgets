############################################################################
# Copyright (c) 2017, Sylvain Corlay, Johan Mabille and Loic Gouarin       #
#                                                                          #
# Distributed under the terms of the BSD 3-Clause License.                 #
#                                                                          #
# The full license is in the file LICENSE, distributed with this software. #
############################################################################

# xwidgets cmake module
# This module sets the following variables in your project::
#
#   xwidgets_FOUND - true if xwidgets found on the system
#   xwidgets_INCLUDE_DIRS - the directory containing xwidgets headers
#   xwidgets_LIBRARY - the library for dynamic linking

@PACKAGE_INIT@

@XWIDGETS_CONFIG_CODE@

include(CMakeFindDependencyMacro)
find_dependency(xtl @xtl_REQUIRED_VERSION@)
find_dependency(xeus @xeus_REQUIRED_VERSION@)
find_dependency(xproperty @xproperty_REQUIRED_VERSION@)

if(NOT TARGET xwidgets AND NOT TARGET xwidgets-static)
  include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@Targets.cmake")

  if (TARGET xwidgets AND TARGET xwidgets-static)
    get_target_property(@PROJECT_NAME@_INCLUDE_DIR xwidgets INTERFACE_INCLUDE_DIRECTORIES)
    get_target_property(@PROJECT_NAME@_LIBRARY xwidgets LOCATION)
    get_target_property(@PROJECT_NAME@_STATIC_LIBRARY xwidgets-static LOCATION)
  elseif (TARGET xwidgets)
    get_target_property(@PROJECT_NAME@_INCLUDE_DIR xwidgets INTERFACE_INCLUDE_DIRECTORIES)
    get_target_property(@PROJECT_NAME@_LIBRARY xwidgets LOCATION)
  elseif (TARGET xwidgets-static)
    get_target_property(@PROJECT_NAME@_INCLUDE_DIR xwidgets-static INTERFACE_INCLUDE_DIRECTORIES)
    get_target_property(@PROJECT_NAME@_STATIC_LIBRARY xwidgets-static LOCATION)
  endif()
endif()
